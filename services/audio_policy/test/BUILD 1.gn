# Copyright (c) 2024-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/test.gni")
import("../../../accessibility.gni")
import("../../../appgallery.gni")
import("../../../av_session.gni")
import("../../../bluetooth_part.gni")
import("../../../config.gni")

audio_framework_root = "../../.."

group("audio_policy_unittest_packages") {
  testonly = true
  deps = [
    ":audio_a2dp_device_unit_test",
    ":audio_a2dp_offload_manager_unit_test",
    ":audio_active_device_unit_test",
    ":audio_adapter_manager_unit_test",
    ":audio_affinity_manager_unit_test",
    ":audio_affinity_parser_test",
    ":audio_background_manager_unit_test",
    ":audio_capturer_session_unit_test",
    ":audio_collaborative_service_unit_test",
    ":audio_concurrency_service_unit_test",
    ":audio_connected_device_unit_test",
    ":audio_core_service_entry_unit_test",
    ":audio_core_service_private_unit_test",
    ":audio_core_service_unit_test",
    ":audio_core_service_utils_unit_test",
    ":audio_device_common_unit_test",
    ":audio_device_lock_unit_test",
    ":audio_device_status_unit_test",
    ":audio_ec_manager_unit_test",
    ":audio_effect_config_parser_test",
    ":audio_effect_config_parser_unit_test",
    ":audio_effect_service_test",
    ":audio_general_manager_unit_test",
    ":audio_global_config_manager_test",
    ":audio_input_thread_unit_test",
    ":audio_interrupt_dfx_collector_unit_test",
    ":audio_interrupt_service_pro_unit_test",
    ":audio_interrupt_service_unit_test",
    ":audio_interrupt_service_second_unit_test",
    ":audio_offload_stream_unit_test",
    ":audio_pipe_manager_unit_test",
    ":audio_pipe_selector_unit_test",
    ":audio_pnp_server_unit_test",
    ":audio_policy_async_action_handler_test",
    ":audio_policy_client_stub_impl_test",
    ":audio_policy_manager_unit_test",
    ":audio_policy_server_handler_unit_test",
    ":audio_policy_server_unit_test",
    ":audio_policy_service_first_unit_test",
    ":audio_policy_service_second_unit_test",
    ":audio_policy_service_third_unit_test",
    ":audio_policy_service_thirdext_unit_test",
    ":audio_policy_state_monitor_test",
    ":audio_policy_utils_unit_test",
    ":audio_recovery_device_unit_test",
    ":audio_router_center_unit_test",
    ":audio_server_definitions_unit_test",
    ":audio_session_service_unit_test",
    ":audio_session_state_monitor_test",
    ":audio_session_unit_test",
    ":audio_socket_thread_unit_test",
    ":audio_spatialization_service_unit_test",
    ":audio_state_manager_unit_test",
    ":audio_stream_collector_pro_unit_test",
    ":audio_stream_collector_unit_test",
    ":audio_strategy_router_parser_unit_test",
    ":audio_setting_provider_unit_test",
    ":audio_tone_parser_unit_test",
    ":audio_usb_manager_unit_test",
    ":audio_volume_manager_unit_test",
    ":audio_zone_service_unit_test",
    ":audio_zone_unit_test",
    ":client_type_manager_unit_test",
    ":device_status_listener_unit_test",
    ":dfx_msg_manager_unit_test",
    ":power_state_listener_unit_test",
    ":privacy_priority_router_unit_test",
    ":stream_filter_router_unit_test",
    ":sle_audio_device_manager_unit_test",
    ":volume_data_maintainer_unit_test",
    ":audio_ipc_serialization_unit_test",
    ":audio_device_manager_unit_test",
  ]
}

module_output_path = "audio_framework/audio_framework_policy/audio_policy"
module_output_path1 =
    "audio_framework/audio_framework_policy/service/audio_policy"

ohos_unittest("audio_zone_service_unit_test") {
  module_out_path = module_output_path
  include_dirs = [
    "./unittest/audio_zone_service_unit_test",
    "../../audio_policy/server/domain/zone/include",
  ]

  use_exceptions = true

  cflags = [
    "-Wall",
    "-Werror",
    "-Wno-macro-redefined",
  ]

  cflags_cc = cflags
  cflags_cc += [ "-fno-access-control" ]

  external_deps = [
    "ability_base:session_info",
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_connect_callback_stub",
    "ability_runtime:ability_context_native",
    "ability_runtime:app_context",
    "ability_runtime:dataobs_manager",
    "ability_runtime:extension_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libprivacy_sdk",
    "access_token:libtokenid_sdk",
    "access_token:libtokensetproc_shared",
    "bounds_checking_function:libsec_shared",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "data_share:datashare_common",
    "data_share:datashare_consumer",
    "drivers_interface_audio:libaudio_proxy_5.0",
    "eventhandler:libeventhandler",
    "hdf_core:libhdf_host",
    "hdf_core:libhdf_ipc_adapter",
    "hdf_core:libhdf_utils",
    "hdf_core:libhdi",
    "hdf_core:libpub_utils",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "init:libbegetutil",
    "ipc:ipc_single",
    "kv_store:distributeddata_inner",
    "media_foundation:media_monitor_client",
    "media_foundation:media_monitor_common",
    "os_account:os_account_innerkits",
    "power_manager:power_setting",
    "power_manager:powermgr_client",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  public_external_deps = [ "ffrt:libffrt" ]

  public_external_deps += [
    "libxml2:libxml2",
    "openssl:libcrypto_shared",
  ]

  sources = [
    "./unittest/audio_zone_service_unit_test/audio_zone_device_unit_test.cpp",
    "./unittest/audio_zone_service_unit_test/audio_zone_unit_test.cpp",
  ]

  deps = [
    "${audio_framework_root}/services/audio_service/idl:audio_framework_interface",
    "../../audio_policy:audio_policy_service_static",
  ]

  configs = [ "../../../services/audio_policy:audio_policy_public_config" ]

  defines = []
  if (audio_framework_enable_unittest_debug) {
    defines += [ "INTERRUPT_UNITTEST" ]
  }

  if (audio_framework_feature_double_pnp_detect) {
    defines += [ "AUDIO_DOUBLE_PNP_DETECT" ]
  }

  if (build_variant == "user") {
    defines += [ "AUDIO_BUILD_VARIANT_USER" ]
  } else if (build_variant == "root") {
    defines += [ "AUDIO_BUILD_VARIANT_ROOT" ]
  }

  if (use_libfuzzer || use_clang_coverage) {
    defines += [ "TEST_COVERAGE" ]
  }

  if (audio_framework_feature_support_os_account) {
    defines += [ "SUPPORT_USER_ACCOUNT" ]
  }

  if (audio_framework_feature_dtmf_tone) {
    defines += [ "FEATURE_DTMF_TONE" ]
  }

  if (audio_framework_feature_usb_audio) {
    defines += [ "USB_ENABLE" ]
    external_deps += [ "usb_manager:usbsrv_client" ]
  }

  if (audio_framework_config_policy_enable) {
    defines += [ "USE_CONFIG_POLICY" ]
    external_deps += [ "config_policy:configpolicy_util" ]
  }

  if (appgallery_part_enable == true) {
    defines += [ "FEATURE_APPGALLERY" ]
  }

  if (bluetooth_part_enable == true) {
    deps +=
        [ "../../../frameworks/native/bluetoothclient:audio_bluetooth_client" ]
    external_deps += [ "bluetooth:btframework" ]
  }

  if (avsession_part_enable == true) {
    external_deps += [
      "ability_base:want",
      "ability_runtime:wantagent_innerkits",
      "av_session:avsession_client",
      "input:libmmi-client",
    ]
  }

  if (audio_framework_feature_input) {
    defines += [ "FEATURE_MULTIMODALINPUT_INPUT" ]
    external_deps += [ "input:libmmi-client" ]
  }

  if (audio_framework_feature_device_manager) {
    defines += [ "FEATURE_DEVICE_MANAGER" ]
    external_deps += [ "device_manager:devicemanagersdk" ]
  }

  if (accessibility_enable == true) {
    cflags += [ "-DACCESSIBILITY_ENABLE" ]
  }

  if (bluetooth_part_enable == true) {
    cflags += [ "-DBLUETOOTH_ENABLE" ]
  }

  if (avsession_part_enable == true) {
    cflags += [ "-DAVSESSION_ENABLE" ]
  }
}