# Copyright (c) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../config.gni")

config("audio_capturer_config") {
  include_dirs = [
    "include",
    "../../../interfaces/inner_api/native/audiocommon/include",
    "../../../interfaces/inner_api/native/audiocapturer/include",
    "../../../frameworks/native/audiostream/include",
    "../../../services/audio_service/client/include",
    "../audioschedule/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]
}

config("audio_external_library_config") {
  include_dirs = [
    "include",
    "../../../interfaces/inner_api/native/audiocapturer/include",
    "../../../interfaces/inner_api/native/audiocommon/include",
    "../../../frameworks/native/audiodefinitions/include",
    "../../../frameworks/native/audiostream/include",
    "../../../services/audio_service/client/include",
    "../../../interfaces/kits/c/common",
  ]
}

ohos_shared_library("audio_capturer") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
  }
  install_enable = true

  configs = [ ":audio_capturer_config" ]

  sources = [
    "../../../services/audio_policy/server/domain/device/src/audio_device_descriptor.cpp",
    "src/audio_capturer.cpp",
    "src/audio_capturer_proxy_obj.cpp",
  ]

  defines = []
  if (audio_framework_feature_inner_capturer) {
    defines += [ "HAS_FEATURE_INNERCAPTURER" ]
  }

  deps = [
    "../../../services/audio_service:audio_common",
    "../../../services/audio_policy:audio_policy_client",
    "../../../services/audio_service:audio_client",
    "../audiodefinitions:audio_definitions",
    "../audioutils:audio_utils",
    "../audioschedule:audio_schedule",
  ]

  if (audio_framework_feature_low_latency) {
    defines += [ "SUPPORT_LOW_LATENCY" ]
  }

  public_configs = [ ":audio_external_library_config" ]

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_single",
    "media_foundation:media_monitor_client",
    "media_foundation:media_monitor_common",
    "samgr:samgr_proxy",
  ]

  version_script = "../../../audio_framework.versionscript"
  innerapi_tags = [ "platformsdk" ]

  part_name = "audio_framework"
  subsystem_name = "multimedia"
}

group("audio_capturer_test_packages") {
  deps = [ ":audio_capturer_test" ]
}

ohos_executable("audio_capturer_test") {
  install_enable = false

  sources = [ "test/example/audio_capturer_test.cpp" ]

  configs = [ ":audio_capturer_config" ]

  deps = [ ":audio_capturer" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]

  part_name = "audio_framework"
  subsystem_name = "multimedia"
}

ohos_executable("audio_inner_capturer_test") {
  install_enable = false
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
  }
  cflags = [
    "-Wall",
    "-Werror",
  ]

  include_dirs = [ "../../../interfaces/inner_api/native/audiocommon/include" ]

  sources = [ "test/example/audio_inner_capturer_test.cpp" ]

  deps = [ ":audio_capturer" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]

  part_name = "audio_framework"
  subsystem_name = "multimedia"
}
